import com.modrinth.minotaur.TaskModrinthUpload
import com.modrinth.minotaur.dependencies.DependencyType
import com.modrinth.minotaur.dependencies.ModDependency
import net.darkhax.curseforgegradle.TaskPublishCurseForge

import java.util.stream.Collectors

plugins {
    id "net.minecraftforge.gradle" version "[6.0,6.2.)"
    id "org.parchmentmc.librarian.forgegradle" version "1.+"
    id "org.spongepowered.mixin" version "0.7-SNAPSHOT"
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "net.darkhax.curseforgegradle" version "1.1.18"
    id "com.modrinth.minotaur" version "2.+"
}

apply plugin: 'java'
apply plugin: 'maven-publish'

tasks.build.dependsOn(tasks.shadowJar)

configurations {
    shadow
    implementation.extendsFrom shadow

    library
    implementation.extendsFrom library

    dataImplementation.extendsFrom implementation
    dataCompileOnly.extendsFrom compileOnly
}

sourceSets {
    data
}

minecraft.runs.configureEach {
    lazyToken('minecraft_classpath') {
        configurations.library.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
    }
}

archivesBaseName = "${mod_name}-forge-${minecraft_version}"

shadowJar {
    configurations = [project.configurations.shadow]
    archiveClassifier.set('')
}
reobf {
    shadowJar {
    }
}

minecraft {
    mappings channel: 'parchment', version: "${minecraft_version}-${parchment_version}-${minecraft_version}"

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        def runArgs = ["-DbygDev=true"]
        client {
            jvmArgs runArgs
            workingDirectory project.file('run')
            args "-mixin.config=${mod_id}.mixins.json", "-mixin.config=${mod_id}_forge.mixins.json"
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Client'
            def output = sourceSets.main.output
            def commonOutput = project(":Common").sourceSets.main.output
            environment("MOD_CLASSES", [output.classesDirs.asPath, output.resourcesDir.path, commonOutput.classesDirs.asPath, commonOutput.resourcesDir.path].join(File.pathSeparator))

            mods {
                modClientRun {
                    source sourceSets.main
                    source project(":Common").sourceSets.main
                }
            }
        }

        server {
            jvmArgs runArgs
            workingDirectory project.file('run')
            args "-mixin.config=${mod_id}.mixins.json", "-mixin.config=${mod_id}_forge.mixins.json"
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Server'
            def output = sourceSets.main.output
            def commonOutput = project(":Common").sourceSets.main.output
            environment("MOD_CLASSES", [output.classesDirs.asPath, output.resourcesDir.path, commonOutput.classesDirs.asPath, commonOutput.resourcesDir.path].join(File.pathSeparator))

            mods {
                modServerRun {
                    source sourceSets.main
                    source project(":Common").sourceSets.main
                }
            }
        }

        data {
            jvmArgs runArgs
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            args '--mod', mod_id, '--all', '--output', project(':Common').file('src/generated/resources'), '--existing', project(':Common').file('src/main/resources'), "-mixin.config=${mod_id}.mixins.json", "-mixin.config=${mod_id}_forge.mixins.json"
            taskName 'Data'
            def output = sourceSets.main.output
            def commonOutput = project(":Common").sourceSets.main.output
            environment("MOD_CLASSES", [output.classesDirs.asPath, sourceSets.data.output.asPath, output.resourcesDir.path, commonOutput.classesDirs.asPath, commonOutput.resourcesDir.path].join(File.pathSeparator))

            mods {
                byg {
                    source sourceSets.main as SourceSet
                    source sourceSets.data as SourceSet
                    source project(":Common").sourceSets.main as SourceSet
                }
            }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    compileOnly project(":Common")
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    implementation fg.deobf("com.github.glitchfiend:TerraBlender-forge:${minecraft_version}-${terrablender_version}")
    implementation fg.deobf("software.bernie.geckolib:geckolib-forge-${minecraft_version}:${geckolib_version}")
    implementation fg.deobf("maven.modrinth:corgilib:${minecraft_version}-${corgilib_version}-forge")

    dataImplementation sourceSets.main.output
}
reg.configureJarTask(shadowJar)

mixin {
    add project(":Common").sourceSets.main, "${mod_id}.refmap.json"
    add sourceSets.main, "${mod_id}_forge.refmap.json"

    config "${mod_id}.mixins.json"
    config "${mod_id}_forge.mixins.json"
}

tasks.withType(JavaCompile).configureEach {
    source(project(":Common").sourceSets.main.allSource)
}

processResources {
    from project(":Common").sourceSets.main.resources

    filesMatching("${mod_id}.mixins.json") {
        expand "refmap_target": "${mod_id}."
    }
    exclude '.cache/**'

    exclude('data/c/tags/**')
    exclude('data/fabric/tags/**')
}

jar.finalizedBy('reobfJar')

jar.archiveClassifier.set('slim')

task forge_checkForRefmap() {
    doLast {
        File file1 = shadowJar.archiveFile.get().asFile
        logger.info("Verifying refmap exists...")
        if (file1.exists()) {
            FileTree files = zipTree(file1)
            Set<String> fileNames = files.getFiles().stream().map(file -> file.getName()).collect(Collectors.toSet())

            String forgeRefmap = "${mod_id}_forge.refmap.json"
            String commonRefmap = "${mod_id}.refmap.json"
            if (!fileNames.contains(forgeRefmap) || !fileNames.contains(commonRefmap)) {
                throw new IllegalStateException("Refmap is missing.")
            }
        } else {
            throw new IllegalStateException("File is missing.")
        }
        logger.info("Refmap found!")
    }
}

String changelogText = projectDir.toPath().getParent().resolve("CHANGELOG.md").getText('UTF-8')

String dependencyText = """
# Required Dependencies


_Links redirect to the dependency's file listing with filters by game version and mod loader applied._

- Terrablender - [Curseforge](https://www.curseforge.com/minecraft/mc-mods/terrablender/files?version=${project.minecraft_version}&gameVersionTypeId=1) or [Modrinth](https://modrinth.com/mod/terrablender/versions?g=${project.minecraft_version}&l=forge) .

- GeckoLib - [Curseforge](https://www.curseforge.com/minecraft/mc-mods/geckolib/files?version=${project.minecraft_version}&gameVersionTypeId=1) or [Modrinth](https://modrinth.com/mod/geckolib/versions?g=${project.minecraft_version}&l=forge) .

- CorgiLib - [Curseforge](https://www.curseforge.com/minecraft/mc-mods/corgilib/files?gameVersionTypeId=1&version=${project.minecraft_version}) or [Modrinth](https://modrinth.com/mod/corgilib/versions?g=${project.minecraft_version}&l=forge) .


# Changelog

"""

task forge_publishCurseForge(type: TaskPublishCurseForge) {
    apiToken = "$System.env.CURSEFORGE_KEY"

    def mainFile = upload(project.forge_curseforge_id, shadowJar)
    mainFile.changelog = dependencyText + changelogText
    mainFile.changelogType = "markdown"
    mainFile.releaseType = "${project.release_type}"
    mainFile.addRequirement("terrablender")
    mainFile.addRequirement("corgilib")
    mainFile.addRequirement("geckolib")
    mainFile.addGameVersion("${project.minecraft_version}")
    mainFile.addModLoader("forge")
}

task forge_publishModrinth (type: TaskModrinthUpload) {
    modrinth {
        token = "$System.env.MODRINTH_KEY"
        projectId = "${project.modrinth_id}"
        versionName = "${archivesBaseName}-${project.version}"
        versionNumber = "${project.minecraft_version}-${project.version}-forge"
        versionType = "${project.release_type}"
        uploadFile = shadowJar
        dependencies = [new ModDependency("ziOp6EO8", DependencyType.REQUIRED), new ModDependency("8BmcQJ2H", DependencyType.REQUIRED), new ModDependency("kkmrDlKT", DependencyType.REQUIRED)]

        String modrinthChangelogText = dependencyText + changelogText.substring(0, changelogText.indexOf("# 2", changelogText.indexOf("# 2") + 1)).trim()
        changelog = modrinthChangelogText

        gameVersions = ["${project.minecraft_version}"]
        loaders = ['forge']
    }
}

task forge_buildAndReleaseCurseforge {}
tasks.forge_checkForRefmap.mustRunAfter tasks.build
tasks.forge_publishCurseForge.mustRunAfter tasks.forge_checkForRefmap
tasks.forge_buildAndReleaseCurseforge.dependsOn(tasks.build, tasks.forge_checkForRefmap, tasks.forge_publishCurseForge)

task forge_buildAndReleaseModrinth {}
tasks.forge_checkForRefmap.mustRunAfter tasks.build
tasks.forge_publishModrinth.mustRunAfter tasks.forge_checkForRefmap
tasks.forge_buildAndReleaseModrinth.dependsOn(tasks.build, tasks.forge_checkForRefmap, tasks.forge_publishModrinth)

task forge_buildAndReleaseModrinthAndCurseforge {}
tasks.forge_checkForRefmap.mustRunAfter tasks.build
tasks.forge_publishCurseForge.mustRunAfter tasks.forge_checkForRefmap
tasks.forge_publishModrinth.mustRunAfter tasks.forge_publishCurseForge
tasks.forge_buildAndReleaseModrinthAndCurseforge.dependsOn(tasks.build, tasks.forge_checkForRefmap, tasks.forge_publishCurseForge, tasks.forge_publishModrinth)
